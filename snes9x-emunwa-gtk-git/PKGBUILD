# Maintainer: Eldred Habert <aur@eldred.fr>

pkgname=snes9x-emunwa-gtk-git
pkgver=1.63.sa1.r0.g77608cb
pkgrel=1
pkgdesc='Port of the Snes9x emulator with network access (GTK version)'
arch=(x86_64)
url='https://github.com/Skarsnik/snes9x-emunwa'
license=(custom)
depends=(gtkmm3 sdl2 libx11 libxrandr libxv libpulse alsa-lib portaudio wayland minizip hicolor-icon-theme)
makedepends=(cmake git python)
source=($pkgname::git+https://github.com/Skarsnik/snes9x-emunwa.git
         git+https://github.com/KhronosGroup/SPIRV-Cross.git
         git+https://github.com/mozilla/cubeb.git
         git+https://github.com/KhronosGroup/glslang.git
         vulkan-headers::git+https://github.com/KhronosGroup/Vulkan-Headers.git
         missing-stdint-h.patch)
sha256sums=(SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP)

pkgver() {
	cd "$pkgname"
	git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$pkgname"
	git submodule init
	for _submodule in external/SPIRV-Cross \
	                  external/cubeb \
	                  external/glslang \
	                  external/vulkan-headers; do
		git config submodule.$_submodule.url "$srcdir/${_submodule##*/}"
	done
	git -c protocol.file.allow=always submodule update external/{SPIRV-Cross,cubeb,glslang,vulkan-headers}

	env -C external/glslang patch -p1 --force <../missing-stdint-h.patch
}

build() {
	CXXFLAGS="$CXXFLAGS -Wno-error=format-security" cmake -S "$pkgname/gtk" -B build -Wno-dev \
		--install-prefix "/usr" \
		-DCMAKE_POLICY_VERSION_MINIMUM=3.5 # SPIRV-Cross specifies its min compat is 3.0, but we're fine with that
	cmake --build build -j"$(nproc)" $MAKEFLAGS
}

package() {
	DESTDIR="$pkgdir" cmake --install build --strip
	cd "$pkgname"
	install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" unix/snes9x.conf.default docs/{control-inputs,controls,snapshots}.txt
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
